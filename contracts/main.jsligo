const tezosUpper = "TEZOS";

const tezosLower = "tezos";

type organizationStatus = ["PendingApproval"] | ["Active"] | ["Frozen"];

type removeMemberParameters = { member: address, orgName: string };

type removeAdminParameters = {
  admin: address,
  orgName: string,
  lastAdmin: option<address>
};

type updateOrganizationParameters = {
  name: string,
  business: string,
  siteUrl: string,
  logoUrl: string,
  ipfsNftUrl: string,
  fundingAddress: option<address>,
  autoRegistration : bool
};

type addAdminParameters = { admin: address, orgName: string };

type joinRequest = {
  orgName: string,
  reason: string
};

type joinOrganizationRequest = { user: address, joinRequest };

type joinOrganizationResponse = {
  membersToApprove: set<address>,
  membersToDecline: set<address>,
  orgName: string
};

type addOrganizationRequest = {
  name: string,
  business: string,
  siteUrl: string,
  logoUrl: string,
  ipfsNftUrl: string,
  fundingAddress: option<address>,
  autoRegistration : bool
};

type organization = {
  name: string,
  business: string,
  siteUrl: string,
  logoUrl: string,
  ipfsNftUrl: string,
  members: big_map<address, unit>,
  admins: set<address>,
  status: organizationStatus,
  memberRequests: set<joinOrganizationRequest>,
  fundingAddress: option<address>,
  autoRegistration : bool
};

type limits = { organizationMax: nat, adminsMax: nat, memberRequestMax: nat };

type storage = {
  tezosOrganization: organization,
  organizations: list<organization>,
  nftAddress: address,
  limits
};

type _return = [list<operation>, storage];

// @entry
const changeLimits = (limits: limits, store: storage): _return => {
  return Set.mem(Tezos.get_sender(), store.tezosOrganization.admins) ? [
      list([]),
      { ...store, limits: limits }
    ] : failwith("you need to be super-administrator to change limits")
};

// @entry
const updateOrganization = (
  orgToUpdate: updateOrganizationParameters,
  store: storage
): _return => {
  if (orgToUpdate.name == tezosUpper) {
    return Set.mem(Tezos.get_sender(), store.tezosOrganization.admins) ? [
        list([Tezos.emit("%organizationUpdated", store.tezosOrganization.name)]),
        {
          ...store,
          tezosOrganization: {
            ...store.tezosOrganization,
            logoUrl: orgToUpdate.logoUrl,
            siteUrl: orgToUpdate.siteUrl,
            business: orgToUpdate.business,
            ipfsNftUrl: orgToUpdate.ipfsNftUrl,
            fundingAddress: orgToUpdate.fundingAddress,
            autoRegistration: orgToUpdate.autoRegistration
          }
        }
      ] : failwith(
        "You need to be part of tezos organization admins to update it"
      )
  } else {
    return match(
      List.find_opt(
        (org: organization) => org.name == orgToUpdate.name,
        store.organizations
      ),
      {
        None: () =>
          failwith(
            "Cannot find an organization with the one passed as parameter"
          ),
        Some: (org) =>
          Set.mem(Tezos.get_sender(), org.admins) ? [
              list([Tezos.emit("%organizationUpdated", org.name)]),
              {
                ...store,
                organizations: List.update_with(
                  (orgItem: organization) => (org.name == orgItem.name),
                  {
                    ...org,
                    logoUrl: orgToUpdate.logoUrl,
                    siteUrl: orgToUpdate.siteUrl,
                    business: orgToUpdate.business,
                    ipfsNftUrl: orgToUpdate.ipfsNftUrl,
                    fundingAddress: orgToUpdate.fundingAddress,
                    autoRegistration: orgToUpdate.autoRegistration
                  },
                  store.organizations
                )
              }
            ] : failwith(
              "You need to be part of organization admins to update an organization"
            )
      }
    )
  }
};

// @entry
const activateOrganization = (orgName: string, store: storage): _return => {
  return match(
    List.find_opt(
      (org: organization) => orgName == org.name,
      store.organizations
    ),
    {
      None: () =>
        failwith(
          "Cannot find an organization with the name passed as parameter"
        ),
      Some: (org) =>
        Set.mem(Tezos.get_sender(), store.tezosOrganization.admins) ? [
            list([Tezos.emit("%organizationActivated", org.name)]),
            {
              ...store,
              organizations: List.update_with(
                (orgItem: organization) => (orgName == orgItem.name),
                { ...org, status: Active() },
                store.organizations
              )
            }
          ] : failwith(
            "You need to be part of Tezos organization to activate an organization"
          )
    }
  )
};

// @entry
const freezeOrganization = (name: string, store: storage): _return => {
  return match(
    List.find_opt(
      (org: organization) => (org.name == name),
      store.organizations
    ),
    {
      None: () =>
        failwith("Cannot find organization with name passed on parameter"),
      Some: (org) =>
        Set.mem(Tezos.get_sender(), store.tezosOrganization.admins) ? [
            list([Tezos.emit("%organizationFrozen", org.name)]),
            {
              ...store,
              organizations: List.update_with(
                (orgItem: organization) => orgItem.name == name,
                { ...org, status: Frozen() },
                store.organizations
              )
            }
          ] : failwith(
            "You need to be part of Tezos organization to remove an organization"
          )
    }
  )
};

// @entry
const addOrganization = (org: addOrganizationRequest, store: storage): _return => {
  //check no one can take TEZOS name

  if (String.length(org.name) == (5 as nat)) {
    let isTezosName: bool = true;
    for (let i = (0 as nat);i < (5 as nat);i = i + (1 as nat)) {
      const letter = String.sub(i, 1 as nat, org.name);
      if (
        letter != String.sub(i, (1 as nat), tezosUpper) &&
          letter != String.sub(i, (1 as nat), tezosLower)
      ) isTezosName = false
    };
    if (isTezosName == true) return failwith(
      "Tezos name for an organization is forbidden"
    )
  };
  let newOrg: organization =
    {
      name: org.name,
      business: org.business,
      siteUrl: org.siteUrl,
      logoUrl: org.logoUrl,
      ipfsNftUrl: org.ipfsNftUrl,
      status: PendingApproval(),
      admins: Set.literal(list([Tezos.get_sender()])) as set<address>,
      members: Big_map.add(Tezos.get_sender(), unit, Big_map.empty) as
        big_map<address, unit>,
      memberRequests: Set.empty as set<joinOrganizationRequest>,
      fundingAddress: org.fundingAddress,
      autoRegistration : org.autoRegistration
    };
  if (List.size(store.organizations) >= store.limits.organizationMax) {
    return failwith(
      "Too many organization, please contact Tezos administrators to increase the limit"
    )
  } else {
    return match(
      List.find_opt(
        (orgItem: organization) => (org.name == orgItem.name),
        store.organizations
      ),
      {
        Some: (_org) =>
          failwith("Organization name already exists and should be unique"),
        None: () =>
          [
            list([Tezos.emit("%organizationAdded", org.name)]),
            {
              ...store,
              organizations: Set.mem(
                Tezos.get_sender(),
                store.tezosOrganization.admins
              ) ? list(
                  [
                    { ...newOrg, status: Active() } as organization,
                    ...store.organizations
                  ]
                ) : list(
                  [{ ...newOrg } as organization, ...store.organizations]
                )
            }
          ]
      }
    )
  }
};

// @entry
const removeOrganization = (name: string, store: storage): _return => {
  return match(
    List.find_opt(
      (org: organization) => (org.name == name),
      store.organizations
    ),
    {
      None: () =>
        failwith("Cannot find organization with name passed on parameter"),
      Some: (org) =>
        Set.mem(Tezos.get_sender(), store.tezosOrganization.admins) ||
          Set.mem(Tezos.get_sender(), org.admins) ? [
            list([]),
            {
              ...store,
              organizations: List.filter_map(
                (orgItem: organization) =>
                  (org.name == orgItem.name ? None() : Some(orgItem)),
                store.organizations
              )
            }
          ] : failwith(
            "You need to be part of organization administrators or Tezos organization to remove an organization"
          )
    }
  )
};

const leaveOrganizationAsAdmin = (
  org: organization,
  lastAdminOpt: option<address>,
  store: storage
): storage => {
  if (!Set.mem(Tezos.get_sender(), org.admins)) return failwith(
    "You need to be an admin to use this function"
  ) else {
    return match(
      lastAdminOpt,
      {
        None: () => {
          if (Set.cardinal(org.admins) == (1 as nat)) failwith(
            "You cannot leave an organization empty without naming another admin from members"
          ) else return {
            ...store,
            organizations: List.filter_map(
              (orgItem: organization) =>
                (orgItem.name == org.name) ? Some(
                    {
                      ...org,
                      admins: Set.remove(Tezos.get_sender(), org.admins)
                    }
                  ) : None() as option<organization>,
              store.organizations
            )
          }
        },
        Some: (lastAdmin) => {
          if (Set.cardinal(org.admins) != (1 as nat)) return failwith(
            "Don't name another admin to replace you if there are enough admins on the organization"
          ) else if (
            Set.cardinal(org.admins) == (1 as nat) &&
              (
                lastAdmin == Tezos.get_sender() ||
                  !Big_map.mem(lastAdmin, org.members)
              )
          ) return failwith(
            "You cannot leave an organization empty naming yourself or not a member"
          ) else {
            const newAdmins =
              Set.cardinal(org.admins) == (1 as nat) ? Set.add(
                  lastAdmin,
                  org.admins
                ) : org.admins;
            return {
              ...store,
              organizations: List.filter_map(
                (orgItem: organization) =>
                  (orgItem.name == org.name) ? Some(
                      {
                        ...org,
                        admins: Set.remove(Tezos.get_sender(), newAdmins)
                      }
                    ) : None() as option<organization>,
                store.organizations
              )
            }
          }
        }
      }
    )
  }
};

// @entry
const removeMember = (rmp: removeMemberParameters, store: storage): _return => {
  const orgOpt =
    List.find_opt(
      (orgItem: organization) => (orgItem.name == rmp.orgName),
      store.organizations
    );
  return match(
    orgOpt,
    {
      None: () =>
        failwith("Cannot find organization with name passed on parameter"),
      Some: (org) => {
        if (Set.mem(rmp.member, org.admins)) failwith(
          "Cannot remove a member who is administrator, use removeAdmin endpoint instead"
        ) else if (
          Set.mem(Tezos.get_sender(), org.admins) ||
            Tezos.get_sender() == rmp.member
        ) {
          return [
            list([]),
            {
              ...store,
              organizations: List.update_with(
                (orgItem: organization) => orgItem.name == org.name,
                { ...org, members: Big_map.remove(rmp.member, org.members) },
                store.organizations
              )
            }
          ]
        } else {
          return failwith(
            "You don't have the rights to remove a member from this organization"
          )
        }
      }
    }
  )
};

// @entry
const removeAdmin = (rap: removeAdminParameters, store: storage): _return => {
  const orgOpt =
    List.find_opt(
      (orgItem: organization) => (orgItem.name == rap.orgName),
      store.organizations
    );
  return match(
    orgOpt,
    {
      None: () =>
        failwith("Cannot find organization with name passed on parameter"),
      Some: (org) => {
        if (Tezos.get_sender() == rap.admin && Set.mem(rap.admin, org.admins)) {
          return [list([]), leaveOrganizationAsAdmin(org, rap.lastAdmin, store)]
        } else if (
          Tezos.get_sender() != rap.admin && Set.mem(rap.admin, org.admins) &&
            Set.mem(Tezos.get_sender(), org.admins)
        ) {
          return [
            list([]),
            {
              ...store,
              organizations: List.filter_map(
                (orgItem: organization) =>
                  orgItem.name == org.name ? Some(
                      { ...org, admins: Set.remove(rap.admin, org.admins) }
                    ) : None() as option<organization>,
                store.organizations
              )
            }
          ]
        } else {
          return failwith(
            "You don't have the rights to remove an administrator from this organization"
          )
        }
      }
    }
  )
};

// @entry
const addAdmin = (aap: addAdminParameters, store: storage): _return => {
  const orgOpt =
    List.find_opt(
      (orgItem: organization) => (orgItem.name == aap.orgName),
      store.organizations
    );
  return match(
    orgOpt,
    {
      None: () =>
        failwith("Cannot find organization with name passed on parameter"),
      Some: (org) => {
        if (Set.cardinal(org.admins) == store.limits.adminsMax) failwith(
          "We have reached admin size limit"
        );
        if (Set.mem(Tezos.get_sender(), org.admins)) {
          return [
            list([]),
            {
              ...store,
              organizations: List.update_with(
                (orgItem: organization) => orgItem.name == org.name,
                {
                  ...org,
                  members: Big_map.add(aap.admin, unit, org.members),
                  admins: Set.add(aap.admin, org.admins)
                },
                store.organizations
              )
            }
          ]
        } else {
          return failwith(
            "You don't have the rights to add an administrator from this organization"
          )
        }
      }
    }
  )
};

// @entry
const addTezosAdmin = (admin: address, store: storage): _return => {
  if (Set.cardinal(store.tezosOrganization.admins) == store.limits.adminsMax) failwith(
    "We have reached admin size limit"
  );
  if (Set.mem(Tezos.get_sender(), store.tezosOrganization.admins)) {
    return [
      list([]),
      {
        ...store,
        tezosOrganization: {
          ...store.tezosOrganization,
          admins: Set.add(admin, store.tezosOrganization.admins)
        }
      }
    ]
  } else {
    return failwith(
      "You don't have the rights to add an administrator from this organization"
    )
  }
};

// @entry
const requestToJoinOrganization = (jr: joinRequest, store: storage): _return => {
  const orgOpt =
    List.find_opt(
      (orgItem: organization) => (orgItem.name == jr.orgName),
      store.organizations
    );
  return match(
    orgOpt,
    {
      None: () =>
        failwith("Cannot find organization with name passed on parameter"),
      Some: (org) => {
        if (org.status != Active()) return failwith(
          "You cannot receive requests until your organization is activated"
        ) else if (
          Set.cardinal(org.memberRequests) == store.limits.memberRequestMax
        ) return failwith("We have reached member request size limit") 
        else if (org.autoRegistration == true){
          const newOrg = {
                  ...org,
                  members: Big_map.add(Tezos.get_sender(), unit, org.members)
                };
          return  [
            list([Tezos.emit(
                  "%orgMemberRequestsUpdated",
                  [Set.literal(list([Tezos.get_sender()])),Set.empty as set<address> , org.name]
                )]),
            {
              ...store,
              organizations: List.update_with(
                (orgItem: organization) => (orgItem.name == org.name),
                newOrg
                ,
                store.organizations
              )
            }
          ]

        } 
        else {
          const newOrg =
            {
              ...org,
              memberRequests: Set.add(
                { user: Tezos.get_sender(), joinRequest: jr },
                org.memberRequests
              )
            };
          return [
            list([Tezos.emit("%joinOrganizationRequest", org.name)]),
            {
              ...store,
              organizations: List.update_with(
                (orgItem: organization) => (orgItem.name == org.name),
                newOrg,
                store.organizations
              )
            }
          ]
        }
      }
    }
  )
};

// @entry
const responseToJoinOrganization = (
  jor: joinOrganizationResponse,
  store: storage
): _return => {
  const orgOpt: option<organization> =
    List.find_opt(
      (org: organization) => (org.name == jor.orgName),
      store.organizations
    );
  return match(
    orgOpt,
    {
      None: () =>
        failwith("Cannot find organization with name passed on parameter"),
      Some: (org) => {
        if (!Set.mem(Tezos.get_sender(), org.admins)) return failwith(
          "You don't have the rights to approve/decline users from this organization"
        ) else {
          let newOrg = { ...org };
          let newStore = { ...store };
          for (const mr of org.memberRequests) {
            if (Set.mem(mr.user, jor.membersToApprove)) {
              newOrg =
                {
                  ...newOrg,
                  members: Big_map.add(mr.user, unit, newOrg.members),
                  memberRequests: Set.remove(mr, newOrg.memberRequests)
                }
            } else if (Set.mem(mr.user, jor.membersToDecline)) {
              newOrg =
                {
                  ...newOrg,
                  memberRequests: Set.remove(mr, newOrg.memberRequests)
                }
            }
          };
          return [
            list(
              [
                Tezos.emit(
                  "%orgMemberRequestsUpdated",
                  [jor.membersToApprove, jor.membersToDecline, org.name]
                )
              ]
            ),
            {
              ...newStore,
              organizations: List.update_with(
                (orgItem: organization) => (orgItem.name == jor.orgName),
                newOrg,
                newStore.organizations
              )
            }
          ]
        }
      }
    }
  )
};

// @entry
const sendMessage = ([to, message]: [string, string], store: storage): _return => {
  return [
    list([Tezos.emit("%message", [Tezos.get_source(), to, message])]),
    store
  ]
};

// @entry
const replyToMessage = ([replyId,replyUser, message]: [nat,string, string], store: storage): _return => {
  return [
    list([Tezos.emit("%reply", [Tezos.get_source(), replyUser,replyId, message])]),
    store
  ]
};